package Producer_Consumer
public
	with Base_Types;
	with SB_Sys;
	with HAMR;
	
	data Coordinate
	end Coordinate;

	data implementation Coordinate.Impl
		-- This is the structure to hold the lat/long/alt values of a coordinate
		subcomponents
			latitude: data Base_Types::Integer_32;
			longitude: data Base_Types::Integer_32;
			altitude: data Base_Types::Integer_32;
	end Coordinate.Impl;

	thread Producer
		features
			trigger: in event data port Base_Types::Boolean;
			msg_out: out event data port Coordinate.Impl;
		properties
			Dispatch_Protocol => Sporadic;
	end Producer;

	thread implementation Producer.Impl
	end Producer.Impl;

	thread Consumer
		features
			msg_in: in event data port Coordinate.Impl {
				SB_Sys::Compute_Entrypoint_Source_Text => ("msg_in_event_handler");
			};
			status: out event data port Base_types::Boolean;
		properties
			Source_Text => ("behavior/consumer.c");
			Dispatch_Protocol => Sporadic;			
		annex agree {**
			-- This is the formalized version of the well-formed requirement from the TA1 tool
			assume Req_Wellformed_LatLong "The Consumer shall only receive well-formed lat-long values" : WELL_FORMED_LAT_LONG(msg_in);
			assume Req_Wellformed_Alt "The Consumer shall only receive well-formed alt values" : WELL_FORMED_ALT(msg_in);
			guarantee "Consumer status shall never be false" : status;
		**};
	end Consumer;

	thread implementation Consumer.Impl	
	end Consumer.Impl;

	processor HW_Proc
	end HW_Proc;
	
	processor implementation HW_Proc.Impl
	end HW_Proc.Impl;
	
	process SW
		features
			input: in event data port Base_Types::Boolean;
			output: out event data port Base_Types::Boolean;
		annex agree {**
			guarantee "Consumer status is never false" : output;
		**};
	end SW;

	process implementation SW.Impl
		subcomponents
			Producer: thread Producer.Impl;
			Consumer: thread Consumer.Impl;
		connections
			c0: port input -> Producer.trigger;
			c1: port Producer.msg_out -> Consumer.msg_in;
			c2: port Consumer.status -> output;
		annex resolute {**
			prove (Req_Wellformed_LatLong(this.Consumer, "Req_Wellformed_LatLong"))
			prove (Req_Wellformed_Alt(this.Consumer, "Req_Wellformed_Alt"))
		**};
	end SW.Impl;
	
	system Sys
		features
			input: in event data port Base_Types::Boolean;
			output: out event data port Base_Types::Boolean;
	end Sys;
	
	system implementation Sys.Impl
		subcomponents
			PROC : processor HW_Proc.Impl;			
			SW: process SW.Impl;
		connections
			c1: port input -> SW.input;
			c2: port SW.output -> output;
		properties
			Actual_Processor_Binding => (reference (PROC)) applies to SW;
			HAMR::Platform => (seL4_TB);			
		annex resolute {**
			check CASE_Tools
		**};
	end Sys.Impl;
	
	annex agree {**
		-- This is the filter spec
		fun WELL_FORMED_LAT_LONG(coord : Coordinate.Impl) : bool = coord.latitude >= -90 and coord.latitude <= 90 and coord.longitude >= -180 and coord.longitude <= 180;
		fun WELL_FORMED_ALT(coord : Coordinate.Impl) : bool = coord.altitude >= 0 and coord.altitude <= 15000;
	**};
	
end Producer_Consumer;