package Waterways_UxAS
public
	with CMASI;
	
	-- Included Main Services
	with AssignmentTreeBranchBoundService_Pkg;
	with AutomationRequestValidatorService_Pkg;	
	with PlanBuilderService_Pkg;
    with RouteAggregatorService_Pkg;
	with RoutePlannerVisibilityService_Pkg;
	with TaskManagerService_Pkg;
	with WaypointPlanManagerService_Pkg;
	with StatusReportService_Pkg;
	with CmasiLineSearchTask_Pkg;
	
	with UxAS;
		
	thread group Waterways extends UxAS::UxAS
		features
			AutomationRequest : in event data port CMASI::AutomationRequest.i;
			AirVehicleConfiguration : in event data port CMASI::AirVehicleConfiguration.i;
			AirVehicleState : in event data port CMASI::AirVehicleState.i;
			LineSearchTask : in event data port CMASI::LineSearchTask.i;
			MissionCommand : out event data port CMASI::MissionCommand.i;
			VehicleActionCommand : out event data port CMASI::VehicleActionCommand.i;
			AutomationResponse : out event data port CMASI::AutomationResponse.i;
	end Waterways;
	
	thread group implementation Waterways.i 
		subcomponents
		
		  -- https://github.com/afrl-rq/OpenUxAS/blob/develop/examples/02_Example_WaterwaySearch/cfg_WaterwaySearch.xml
		  -- Removed: Test_SimulationTime
		  StatusReportService: thread StatusReportService_Pkg::StatusReportService;
		  TaskManagerService: thread TaskManagerService_Pkg::TaskManagerService;
		  AutomationRequestValidatorService: thread AutomationRequestValidatorService_Pkg::AutomationRequestValidatorService;
		  RoutePlannerVisibilityService: thread RoutePlannerVisibilityService_Pkg::RoutePlannerVisibilityService;
		  RouteAggregatorService: thread RouteAggregatorService_Pkg::RouteAggregatorService;
		  AssignmentTreeBranchBoundService: thread AssignmentTreeBranchBoundService_Pkg::AssignmentTreeBranchBoundService;
		  PlanBuilderService: thread PlanBuilderService_Pkg::PlanBuilderService;
		  WaypointPlanManagerService: thread WaypointPlanManagerService_Pkg::WaypointPlanManagerService;       
		  CmasiLineSearchTask: thread CmasiLineSearchTask_Pkg::CmasiLineSearchTask;
		  
		connections
			
			------------------------------------------------------------------------------------------------------------------
			-- Connections between the ground station (or controller) and the UxAS instances, and connections between UxAS
			-- services. The connections reflect the message sequence flow defined for this example. The message sequence flow 
			-- has 4 states: Initialization, Assignment, Implementation, and Execution. The connections are group and labeled 
			-- based on the state(s) in which the messages occur.
			------------------------------------------------------------------------------------------------------------------
			
			------------------
			-- Primary Inputs
			------------------
			c_AVCfg_01 : port AirVehicleConfiguration -> RouteAggregatorService.AirVehicleConfiguration;
			c_AVCfg_02 : port AirVehicleConfiguration -> RoutePlannerVisibilityService.AirVehicleConfiguration;
			c_AVCfg_03 : port AirVehicleConfiguration -> TaskManagerService.AirVehicleConfiguration;
			c_AVCfg_04 : port AirVehicleConfiguration -> AutomationRequestValidatorService.AirVehicleConfiguration;
			c_AVCfg_05 : port AirVehicleConfiguration -> CmasiLineSearchTask.AirVehicleConfiguration;
			
			c_AVSt_06 : port AirVehicleState -> WaypointPlanManagerService.AirVehicleState;
			c_AVSt_07 : port AirVehicleState -> RoutePlannerVisibilityService.AirVehicleState;
			c_AVSt_08 : port AirVehicleState -> PlanBuilderService.AirVehicleState;
			c_AVSt_09 : port AirVehicleState -> TaskManagerService.AirVehicleState;
			c_AVSt_10 : port AirVehicleState -> AutomationRequestValidatorService.AirVehicleState;
			c_AVSt_11 : port AirVehicleState -> RouteAggregatorService.AirVehicleState;
			c_AVSt_12 : port AirVehicleState -> CmasiLineSearchTask.AirVehicleState;
			
			c_AVSt_01 : port AutomationRequest -> AutomationRequestValidatorService.AutomationRequest;
            	
            c_LST_01 : port LineSearchTask -> TaskManagerService.LineSearchTask;
            
            -------------------------------------
            -- AutomationRequestValidatorService
            -------------------------------------
            c_UAR_01 : port AutomationRequestValidatorService.UniqueAutomationRequest -> RouteAggregatorService.UniqueAutomationResquest;           
            c_UAR_02 : port AutomationRequestValidatorService.UniqueAutomationRequest -> AssignmentTreeBranchBoundService.UniqueAutomationRequest;
            c_UAR_03 : port AutomationRequestValidatorService.UniqueAutomationRequest -> PlanBuilderService.UniqueAutomationRequest;                       
            c_UAR_04 : port AutomationRequestValidatorService.UniqueAutomationRequest -> CmasiLineSearchTask.UniqueAutomationRequest;
            -- EGM: All the sequence charts in the UxAS docks show the below connection, but it is _not_ in the code.
            -- The TaskManagerService does _not_ subscribe to UniqueAutomationRequest though it does send that message.
            -- c_UAR_05 : port AutomationRequestValidatorService.UniqueAutomationRequest -> TaskManagerService.UniqueAutomationRequest;
            
            c_AR_01 : port AutomationRequestValidatorService.AutomationResponse -> AutomationResponse;
            c_AR_02 : port AutomationRequestValidatorService.AutomationResponse -> WaypointPlanManagerService.AutomationResponse;
            
            -----------------------
            -- CmasiLineSearchTask
            -----------------------
            c_TPO_01 : port CmasiLineSearchTask.TaskPlanOptions -> RouteAggregatorService.TaskPlanOptions;
            c_TPO_02 : port CmasiLineSearchTask.TaskPlanOptions -> AssignmentTreeBranchBoundService.TaskPlanOptions;
            
            c_VAC_01 : port CmasiLineSearchTask.VehicleActionCommand -> VehicleActionCommand;
            
            c_TIR_01 : port CmasiLineSearchTask.TaskImplementationResponse -> PlanBuilderService.TaskImplementationResponse;
            
            c_CLST_RPR_01 : port CmasiLineSearchTask.RoutePlanRequest -> RoutePlannerVisibilityService.RoutePlanRequest_task;
            
            -------------------------
            -- RouteAggregatorService
            -------------------------
			c_RPR_01 : port RouteAggregatorService.RoutePlanRequest -> RoutePlannerVisibilityService.RoutePlanRequest_ragg;
            c_ACM_01 : port RouteAggregatorService.AssignmentCostMatrix -> AssignmentTreeBranchBoundService.AssignmentCostMatrix;
                 
			---------------------------------------
			-- AssignmentTreeBranchAndBoundService
			---------------------------------------
			c_TAS_01 : port AssignmentTreeBranchBoundService.TaskAssignmentSummary -> PlanBuilderService.TaskAssignmentSummary;
       		
        	-------------------
       		-- PlanBuilderService
       		-------------------
       		c_PBS_TIR_01: port PlanBuilderService.TaskImplementationRequest -> CmasiLineSearchTask.TaskImplementationRequest;
  			c_impl_01 : port PlanBuilderService.UniqueAutomationResponse -> AutomationRequestValidatorService.UniqueAutomationResponse;
			
			---------------------------------
			-- RoutePlannerVisibilityService
			---------------------------------
		
       		-------------
       		-- Execution
       		-------------
       		c_exec_01 : port WaypointPlanManagerService.MissionCommand_out -> MissionCommand;
				
	end Waterways.i;
	
end Waterways_UxAS;